command /start:
	trigger:
		if {inGame::%player%} is true:
			send "Game already started use /stopGame" to player
		else:
			set {inGame::%player%} to true
			send "<#0AFA7C>| <#30D47E>S<#43C07E>t<#55AD7F>a<#689A80>r<#7B8781>t<#8E7381>i<#A16082>n<#B44D83>g <#D92684>u<#EC1385>p <#FF007C>M<#FF0071>i<#FF0067>n<#FF005D>e <#FF0048>R<#FF003E>u<#FF0034>s<#FF002A>h <#FF0015>|" to player
			#Picks random location and makes sure there is not water below the player (prevents spawning in ocean)
			while {spawningLocation::X::%player%} is not set:
				set {spawningLocation::X::%player%} to a random integer between -100000 and 1000000
				set {spawningLocation::Z::%player%} to a random integer between -100000 and 1000000

				set {_waterCheck} to location at {spawningLocation::X::%player%}, 62, {spawningLocation::Z::%player%} in world "world"
				set {_airCheck} to location at {spawningLocation::X::%player%}, {spawningHeight}, {spawningLocation::Z::%player%} in world "world"
				if block at {_waterCheck} is water:
					delete {spawningLocation::X::%player%}
				
				if block at {_airCheck} is not air:
					delete {spawningLocation::X::%player%}
				
				if {debugMode::%player%} is true:
					if {spawningLocation::X::%player%} is not set:
						send "&c&lInvalid Location" to player
				wait 1 tick
			
			wait 5 seconds # waits for server to catch up before starting count down
			send title "&c&lStarting in 5..." to player
			wait 1 second
			send title "&c&lStarting in 4..." to player
			wait 1 second
			send title "&c&lStarting in 3..." to player
			apply blindness without any particles to the player for 8 seconds
			wait 1 second
			send title "&c&lStarting in 2..." to player
			wait 1 second
			send title "&c&lStarting in 1..." to player
			wait 1 second
			send title "&c&lGood Luck" to player
			if {settings::startingMusic::%player%} is true:
				playRandomMusic(player)
			set time of world "world" to 8:00

			# Slow falling /tp
			apply slow falling potion without any particles to the player for 90 seconds
			teleport player to location({spawningLocation::X::%player%}, {spawningHeight}, {spawningLocation::Z::%player%})
			asyncAircheck(player)


			execute console command "/gamerule block_drops false"
			execute console command "/gamerule mob_drops false"
			execute console command "/gamerule advance_time true"
			set weather to clear
			heal player
			feed player
			clear player's inventory
			
			# Items
			give unbreakable stone pickaxe named {pickaxe::tier::stone} and 16 cooked beef to player
			set {pickaxe::tier::%player%} to "stone"

			# Boss bar
			set {pointBar::%player%} to boss bar with id "01" with title "&a&lPoints: 0" with color green with progress 0
			set bar style of {pointBar::%player%} to solid_bar
			if {debugMode::%player%} is true:
				set {points::%player%} to 24000000
				set bar title of {pointBar::title::%player%} to "&a&lPoints: %{points::%player%}%"
			set {pointBar::progress::%player%} to 0
			add player to {pointBar::%player%}

			# Sets upgrades to defaults
			add 1 to {currentGameStats::%player%::gamesStarted}
			# Starts the difficulty timer (see difficulties.sk)
			difficultyBegin(player)

function asyncAircheck(p: player):
	while block below {_p} is air:
		wait 10 ticks
	remove slow falling from {_p}'s active potion effects