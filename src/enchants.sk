function veinMiner(p: player, b: block, originalType: item type):
	chance of {veinMiner::chance::%{customEnchantTier::%{_p}%::veinMiner}%}:
		loop all blocks in radius 1.75 around {_b}:
			if type of loop-block is {_originalType}:
				set {_loopBlock} to loop-block
				addPoints({_p}, {_loopBlock}, true)
				set loop-block to air
				make 50 of block particle using {_originalType} at location of {_loopBlock}
				play sound (break sound of {_originalType}) at location of {_loopBlock}
				wait 3 ticks
				veinMiner({_p}, {_loopBlock}, {_originalType})
				

function streak(p: player) :: number:
	chance of {streak::chance::%{customEnchantTier::%{_p}%::streak}%}:
		if {streak::timer::%{_p}%} is more than or equal to 10 seconds ago:
			if {streak::value::%{_p}%} is less than {streak::max}:
				add 0.010 to {streak::value::%{_p}%}
		else:
			set {streak::value::%{_p}%} to 0.001

		set {streak::timer::%{_p}%} to now
		return {streak::value::%{_p}%}

function oreDuplicate(p: player, b: block):
	chance of {oreDuplicate::chance::%{customEnchantTier::%{_p}%::oreDuplicate}%}:
		play sound "BLOCK_AMETHYST_BLOCK_STEP" with volume {volume} at location of {_b}
		make 10 of happy villager at location of {_b} with offset vector(0.35, 0.35, 0.35)
		set {_new} to type of {_b}
		wait 1 tick
		set block at location of {_b} to {_new}

function luckyMiner(p: player, b: block) :: text:
	chance of {luckyMiner::chance::%{customEnchantTier::%{_p}%::luckyMiner}%}:
		set {_currentType} to "%type of {_b}%"
		set {_typeFound} to false
		loop {ores::*}:
			if {_typeFound} is false:
				if loop-value is {_currentType}:
					set {_typeFound} to true
					send "found" to {_p}
			else:
				set {_typeFound} to false
				play sound "ENTITY_TURTLE_SHAMBLE" with volume {volume} at {_b}
				set {_newType} to loop-value
				return {_newType}
	return "none"

function lootBoxCollector(p: player):
	chance of {lootBoxCollector::chance::%{customEnchantTier::%{_p}%::lootBoxCollector}%}:
		set {_chance} to a random integer between 1 and 100
		if {_chance} >= 50: # 50% chance
			give {_p} {lootBox::item::common}
			send "+1 %name of {lootBox::item::common}%" to {_p}

		else if {_chance} >= 15: # 35% chance
			give {_p} {lootBox::item::rare}
			send "+1 %name of {lootBox::item::rare}%" to {_p}

		else: # 15% chance
			give {_p} {lootBox::item::legendary}
			send "+1 %name of {lootBox::item::legendary}%" to {_p}

function hungryMiner(p: player):
	chance of {hungryMiner::chance::%{customEnchantTier::%{_p}%::hungryMiner}%}:
		add 1 to {_p}'s food level
		
function fireball(p: player):
	chance of {fireball::chance::%{customEnchantTier::%{_p}%::fireball}%}:
		if {cooldown::fireball::%{_p}%} is not set:
			spawn fireball 1 block infront of {_p} with nbt from "{ExplosionPower:%{fireball::size::%{customEnchantTier::%{_p}%::fireball}%}%}"
			set {cooldown::fireball::%{_p}%} to now
		if difference between now and {cooldown::fireball::%{_p}%} is more than 60 seconds:
			set {cooldown::fireball::%{_p}%} to now
			spawn fireball 1 block infront of {_p} with nbt from "{ExplosionPower:%{fireball::size::%{customEnchantTier::%{_p}%::fireball}%}%}"
		else:
			send "Cooldown still active please wait!" to {_p}

on rightclick holding a stone pickaxe, iron pickaxe, diamond pickaxe, or netherite pickaxe:
	if {inGame::%player%}:
		fireball(player)

