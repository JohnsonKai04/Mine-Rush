function veinMiner(p: player, b: block):
	chance of {veinMiner::chance::%{customEnchantTier::%{_p}%::veinMiner}%}:
		set {_blockType} to type of {_b}
		loop all blocks in radius 1.75 around {_b}:
			if "%type of loop-block%" is "%type of {_b}%": # Type of event block not only stores the name of the event block but also the NBT tags of said block. This makes it so lit redstone and redstone are different
				add loop-block to {_blocks::*}

		loop {_blocks::*}:
			wait 2 ticks
			if loop-value is not air:
				set {_loopBlock} to loop-value
				addPoints({_p}, {_loopBlock})
				make 50 of block particle using {_blockType} at location of {_loopBlock}
				play sound (break sound of {_blockType}) at location of {_loopBlock}
				set block at location of {_loopBlock} to air
				

function streak(p: player) :: number:
	chance of {streak::chance::%{customEnchantTier::%{_p}%::streak}%}:
		if {streak::timer::%{_p}%} is more than or equal to 10 seconds ago:
			if {streak::value::%{_p}%} is less than {streak::max}:
				add 0.010 to {streak::value::%{_p}%}
		else:
			set {streak::value::%{_p}%} to 0.001

		set {streak::timer::%{_p}%} to now
		return {streak::value::%{_p}%}

function oreDuplicate(p: player, b: block):
	chance of {oreDuplicate::chance::%{customEnchantTier::%{_p}%::oreDuplicate}%}:
		play sound "BLOCK_AMETHYST_BLOCK_STEP" with volume {settings::volume::%{_p}%} at location of {_b}
		make 10 of happy villager at location of {_b} with offset vector(0.35, 0.35, 0.35)
		set {_new} to type of {_b}
		wait 1 tick
		set block at location of {_b} to {_new}

function luckyMiner(p: player, b: block) :: text:
	set {_currentType} to "%type of {_b}%"
	chance of {luckyMiner::chance::%{customEnchantTier::%{_p}%::luckyMiner}%}:
		set {_typeFound} to false
		loop {ores::*}:
			if {_typeFound} is false:
				if loop-value is {_currentType}:
					set {_typeFound} to true
			else:
				set {_typeFound} to false
				play sound "ENTITY_TURTLE_SHAMBLE" with volume {settings::volume::%{_p}%} at {_b}
				set {_newType} to loop-value
				return {_newType}
	return {_currentType}

function lootBoxCollector(p: player):
	chance of {lootBoxCollector::chance::%{customEnchantTier::%{_p}%::lootBoxCollector}%}:
		set {_chance} to a random integer between 1 and 100
		if {_chance} >= 50: # 50% chance
			give {_p} {lootBox::item::common}
			send "+1 %name of {lootBox::item::common}%" to {_p}

		else if {_chance} >= 15: # 35% chance
			give {_p} {lootBox::item::rare}
			send "+1 %name of {lootBox::item::rare}%" to {_p}

		else: # 15% chance
			give {_p} {lootBox::item::legendary}
			send "+1 %name of {lootBox::item::legendary}%" to {_p}

function hungryMiner(p: player):
	chance of {hungryMiner::chance::%{customEnchantTier::%{_p}%::hungryMiner}%}:
		add 1 to {_p}'s food level
		
function fireball(p: player):
	chance of {fireball::chance::%{customEnchantTier::%{_p}%::fireball}%}:
		if {cooldown::fireball::%{_p}%} is not set:
			spawn fireball 1 block above and 2 blocks infront of {_p} with nbt from "{ExplosionPower:%{fireball::size::%{customEnchantTier::%{_p}%::fireball}%}%}"
			set {cooldown::fireball::%{_p}%} to now
		if difference between now and {cooldown::fireball::%{_p}%} is more than 60 seconds:
			spawn fireball 1 block above and 2 blocks infront of {_p} with nbt from "{ExplosionPower:%{fireball::size::%{customEnchantTier::%{_p}%::fireball}%}%}"
			set {cooldown::fireball::%{_p}%} to now
		else:
			set {_difference} to difference between {cooldown::fireball::%{_p}%} and now
			set {_remaining} to difference between {_difference} and 60 seconds
			playErrorSound({_p})
			send "<#F10C31>C<#EE0C31>o<#EB0D30>o<#E90D30>l<#E60E30>d<#E30E2F>o<#E00F2F>w<#DD0F2F>n <#D8102E>s<#D5112E>t<#D2112E>i<#CF122D>l<#CD122D>l <#C7132C>a<#C4142C>c<#C1142C>t<#BF152B>i<#BC152B>v<#B9162B>e <#B3172A>p<#B1172A>l<#AE1829>e<#AB1829>a<#A81929>s<#A51929>e <#A01A28>w<#9D1B28>a<#9A1B27>i<#971C27>t<#951C27>: <#8F1D26>%{_remaining}%!" to {_p}

on rightclick holding a stone pickaxe, iron pickaxe, diamond pickaxe, or netherite pickaxe:
	if {inGame::%player%} is true:
		fireball(player)