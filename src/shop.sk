# Param - _p: player that is purchasing from the shop
# Param - _item: name of the item that the player is buying, this will be used to match the item with a price
# Return - bool: returns true if the purcahse was sucessful but false if the buyer does not have enough money
function chargePlayer(p: player, item: text) :: boolean:
	if {cost::%{_item}%} <= {points::%{_p}%}:
		# Subtracts cost from balance
		set {points::%{_p}%} to {points::%{_p}%} - {cost::%{_item}%}

		# Adds to stat
		add {cost::%{_item}%} to {stats::SpentPoints::%{_p}%}
		play sound "entity.experience_orb.pickup" with volume {volume} to {_p}

		# Updates boss bar
		set bar title of {pointBar::%{_p}%} to "&a&lPoints: %{points::%{_p}%}%"
		return true
	else:
		play sound "block.note_block.bass" with volume {volume} to {_p}
		send "&4&lERROR: Insufficient funds" to {_p}
		return false

# Param - _p: player that is buying armor
# Param - _item: level of armor they are buying
# gives player unbreakable armor with trims, auto equips, updates the variable with their current armor level
function purchaseArmor(p: player, item: text):
	if chargePlayer({_p}, {_item}) is true:
		if {_item} is "leather armor":
			set {armor::tier::%{_p}%} to "leather"
			set {_helmet} to unbreakable leather helmet
			set {_chestplate} to unbreakable leather chestplate
			set {_leggings} to unbreakable leather leggings
			set {_boots} to unbreakable leather boots
		else if {_item} is "iron armor":
			set {armor::tier::%{_p}%} to "iron"
			set {_helmet} to unbreakable iron helmet
			set {_chestplate} to unbreakable iron chestplate
			set {_leggings} to unbreakable iron leggings
			set {_boots} to unbreakable iron boots
		else if {_item} is "diamond armor":
			set {armor::tier::%{_p}%} to "diamond"
			set {_helmet} to unbreakable diamond helmet
			set {_chestplate} to unbreakable diamond chestplate
			set {_leggings} to unbreakable diamond leggings
			set {_boots} to unbreakable diamond boots

		add armor trim using gold_material with spire_pattern to armor trim of {_helmet}
		add armor trim using gold_material with spire_pattern to armor trim of {_chestplate}
		add armor trim using gold_material with spire_pattern to armor trim of {_leggings}
		add armor trim using gold_material with spire_pattern to armor trim of {_boots}
		loop enchantments of {_p}'s helmet:
			set {_enchant::%loop-iteration%} to loop-value
		set {_p}'s helmet to {_helmet}
		set {_p}'s chestplate to {_chestplate}
		set {_p}'s leggings to {_leggings}
		set {_p}'s boots to {_boots}
		loop {_enchant::*}:
			enchant {_p}'s armor with {_enchant::%loop-iteration%}
		enchant {_p}'s armor with curse of binding 1
	make {_p} execute command "/shop"

# Param - _p: player buying
# Param - _item: level of pickaxe they are buying
# loops players inventory to find the pickaxe they have based off of the name, removes the old pickaxes and gives the new, updates the variable with their current pickaxe level
function purchasePickaxe(p: player, item: text):
	if chargePlayer({_p}, {_item}) is true:
		loop all items in the inventory of {_p}:
			if name of loop-item is {pickaxe::tier::stone}:
				loop enchantments of loop-item:
					set {_enchant::%loop-iteration-2%} to loop-value-2
				set loop-item to air
				give unbreakable iron pickaxe named {pickaxe::tier::iron} to {_p}
				loop {_enchant::*}:
					enchant loop-item with {_enchant::%loop-iteration-2%}

				loop {customEnchantTier::%{_p}%::*}:
					set {_romans::*} to convertToRoman({customEnchantTier::%{_p}%::%loop-index%})
					set {_roman} to {_romans::2}
					if loop-value-2 is 1:
						add "&b%loop-index% %{_roman}%" to lore of loop-item

				set {pickaxe::tier::%{_p}%} to "iron"
				stop loop
			else if name of loop-item is {pickaxe::tier::iron}:
				loop enchantments of loop-item:
					set {_enchant::%loop-iteration-2%} to loop-value-2
				set loop-item to air
				give unbreakable diamond pickaxe named {pickaxe::tier::diamond} to {_p}
				loop {_enchant::*}:
					enchant loop-item with {_enchant::%loop-iteration-2%}

				loop {customEnchantTier::%{_p}%::*}:
					set {_romans::*} to convertToRoman({customEnchantTier::%{_p}%::%loop-index%})
					set {_roman} to {_romans::2}
					if loop-value-2 is 1:
						add "&b%loop-index% %{_roman}%" to lore of loop-item

				set {pickaxe::tier::%{_p}%} to "diamond"
				stop loop
			else if name of loop-item is {pickaxe::tier::diamond}:
				loop enchantments of loop-item:
					set {_enchant::%loop-iteration-2%} to loop-value-2
				set loop-item to air
				give unbreakable netherite pickaxe named {pickaxe::tier::netherite} to {_p}
				loop {_enchant::*}:
					enchant loop-item with {_enchant::%loop-iteration-2%}

				loop {customEnchantTier::%{_p}%::*}:
					set {_romans::*} to convertToRoman({customEnchantTier::%{_p}%::%loop-index%})
					set {_roman} to {_romans::2}
					if loop-value-2 is 1:
						add "&b%loop-index% %{_roman}%" to lore of loop-item

				set {pickaxe::tier::%{_p}%} to "netherite"
				stop loop
	make {_p} execute command "/shop"

# Param - _p: player buying
# Param - _item: sword they are buying
# loops players inventory to find the sword they have based off of the name, removes the old sword and gives the new, updates the variable with their current sword level
function purchaseWeapon(p: player, item: text):
	if chargePlayer({_p}, {_item}) is true:
		if {sword::tier::%{_p}%} is not set:
			give unbreakable stone sword named {sword::tier::stone} to {_p}
			set {sword::tier::%{_p}%} to "stone"
		else:
			loop all items in the inventory of {_p}:
				if name of loop-item is {sword::tier::stone}:
					loop enchantments of loop-item:
						set {_enchant::%loop-iteration-2%} to loop-value-2
					set loop-item to air
					give unbreakable iron sword named {sword::tier::iron} to {_p}
					loop {_enchant::*}:
						enchant loop-item with {_enchant::%loop-iteration-2%}
					set {sword::tier::%{_p}%} to "iron"
					stop loop
				else if name of loop-item is {sword::tier::iron}:
					loop enchantments of loop-item:
						set {_enchant::%loop-iteration-2%} to loop-value-2
					set loop-item to air
					give unbreakable diamond sword named {sword::tier::diamond} to {_p}
					loop {_enchant::*}:
						enchant loop-item with {_enchant::%loop-iteration-2%}
					set {sword::tier::%{_p}%} to "diamond"
					stop loop
				else if name of loop-item is {sword::tier::diamond}:
					loop enchantments of loop-item:
						set {_enchant::%loop-iteration-2%} to loop-value-2
					set loop-item to air
					give unbreakable netherite sword named {sword::tier::netherite} to {_p}
					loop {_enchant::*}:
						enchant loop-item with {_enchant::%loop-iteration-2%}
					set {sword::tier::%{_p}%} to "netherite"
					stop loop
	make {_p} execute command "/shop"

function purchaseEnchant(p: player, item: text, enchantName: text):
	if chargePlayer({_p}, {_enchantName}) is true:
		add 1 to {enchantTier::%{_enchantName}%::%{_p}%}
		set {cost::%{_enchantName}%} to round({cost::%{_enchantName}%} * 1.5)  # NOT COMPATIBLE WITH MULTIPLAYER
		set {_enchantment} to "%{_enchantName}% %{enchantTier::%{_enchantName}%::%{_p}%}%" parsed as enchantment type

		# Checks if item is armor if not enchants all other items
		if {_item} is "armor":
			enchant {_p}'s armor with {_enchantment}
		else:
			loop all items in the inventory of {_p}:
				loop {%{_item}%::tier::*}:
					if name of loop-item is loop-value-2:
						enchant loop-item with {_enchantment}

	make {_p} execute command "/shop"

function purchaseCustomEnchant(p: player, enchantName: text):
	if chargePlayer({_p}, {_enchantName}) is true:
		add 1 to {customEnchantTier::%{_p}%::%{_enchantName}%}
		set {_romans::*} to convertToRoman({customEnchantTier::%{_p}%::%{_enchantName}%})
		set {_pRoman} to {_romans::2}
		set {_roman} to {_romans::1}
		
		set {cost::%{_enchantName}%} to round({cost::%{_enchantName}%} * 2)
		loop all items in the inventory of {_p}:
			loop {pickaxe::tier::*}:
				if name of loop-item is loop-value-2:
					if {customEnchantTier::%{_p}%::%{_enchantName}%} is 1:
						add "&b%{_enchantName}% %{_roman}%" to lore of loop-item
						if {debugMode}:
							send "loop value found and enchant applied" to {_p}
						stop loop
					else:
						loop lore of loop-item:
							if loop-value-3 is "&b%{_enchantName}% %{_roman}%":
								replace loop-value-3 in lore of loop-item with "&b%{_enchantName}% %{_pRoman}%"
								if {debugMode}:
									send "loop value found and enchant replaced" to {_p}
								stop loop
	else:
		send "Charge unscucessful" to {_p}
	make {_p} execute command "/shop"

function convertToRoman(value: number) :: texts:
	if {_value} is 1:
		set {_roman} to "I"
	else if {_value} is 2:
		set {_pRoman} to "I"
		set {_roman} to "II"
	else if {_value} is 3:
		set {_pRoman} to "II"
		set {_roman} to "III"
	else if {_value} is 4:
		set {_pRoman} to "III"
		set {_roman} to "IV"
	else if {_value} is 5:
		set {_pRoman} to "IV"
		set {_roman} to "V"
	else:
		broadcast "error no roman found"
	set {_return::*} to ({_pRoman},{_roman})
	return {_return::*}


# Param - _P: player buying
# Param - _item: item they are buying
# Param - _count: the amount they are buying (pre determined by the /shop)
# Buys general items that are not related to upgrades
function purchaseItem(p: player, item: text, count: number):
	if chargePlayer({_p}, {_item}) is true:
		give {_p} {_count} of {_item} parsed as item
	make {_p} execute command "/shop"

function purcahseCustomItem(p: player, item: item, cItem: text):
	if chargePlayer({_p}, {_cItem}) is true:
		if {_cItem} is "ghead":
			give {_p} skull of ({goldenSkullPlayerName} parsed as offline player) named {customItem::ghead::name} with lore {customItem::ghead::lore}
		else if {_cItem} is "grappling hook":
			give {_p} {_item} named {customItem::grappling hook::name} with lore {customItem::grappling hook::lore}

command /shop:
	trigger:
		if {inGame::%player%} is false:
			send "This command can only be used when the game has been started" to player
			stop

		open chest inventory with 6 rows named "&6&lShop" to player
		set {_inv} to player's current inventory
		set slot 0 of {_inv} to 16 cooked beef named "&r&lSteak" with lore "&7Cost: %{cost::cooked beef}% Points"
		set slot 1 of {_inv} to 16 ladders named "&r&lLadder" with lore "&7Cost: %{cost::ladder}% Points"
		set slot 2 of {_inv} to 8 wind charge named "&r&lWind charges" with lore "&7Cost: %{cost::wind charge}% Points"
		set slot 3 of {_inv} to 64 andesite named "&r&lAndesite" with lore "&7Cost: %{cost::andesite}% Points"
		set slot 4 of {_inv} to 32 torch named "&r&lTorches" with lore "&7Cost: %{cost::torch}% Points"
		set slot 5 of {_inv} to 1 water bucket named "&r&lWater Bucket" with lore "&7Cost: %{cost::water bucket}% Points"
		set slot 6 of {_inv} to 32 scaffolding named "&r&lScaffolding" with lore "&7Cost: %{cost::scaffolding}% Points"
		set slot 7 of {_inv} to 8 tnt named "&r&lTNT" with lore "&7Cost: %{cost::tnt}% Points"
		set slot 8 of {_inv} to shield named "&r&lShield" with lore "&7Cost: %{cost::shield}% Points"

		# Custom items
		set slot 19 of {_inv} to fishing rod named "&r&lGrappling Hook" with lore "&7Cost: %{cost::grappling hook}% Points"
		set slot 20 of {_inv} to golden apple named "&r&lGolden Head" with lore "&7Cost: %{cost::ghead}% Points"

		# Pickaxes
		if {pickaxe::tier::%player%} is "stone":
			set slot 28 of {_inv} to iron pickaxe named "&r&lIron Pickaxe" with lore "&7Cost: %{cost::iron pickaxe}% Points"
		else if {pickaxe::tier::%player%} is "iron":
			set slot 28 of {_inv} to diamond pickaxe named "&r&lDiamond Pickaxe" with lore "&7Cost: %{cost::diamond pickaxe}% Points"
		else if {pickaxe::tier::%player%} is "diamond":
			set slot 28 of {_inv} to netherite pickaxe named "&r&lNetherite Pickaxe" with lore "&7Cost: %{cost::netherite pickaxe}% Points"

		# Pickaxe enchants
		set slot 29 of {_inv} to enchanted book named "&r&lEfficiency" with lore "&7Cost: %{cost::efficiency}% Points"
		# Pickaxe enchants custom
		set slot 30 of {_inv} to enchanted book named "&r&lVein Miner" with lore "&7Cost: %{cost::vein miner}% Points"
		set slot 31 of {_inv} to enchanted book named "&r&lStreak" with lore "&7Cost: %{cost::streak}% Points"
		set slot 32 of {_inv} to enchanted book named "&r&lOre Duplicate" with lore "&7Cost: %{cost::ore duplicate}% Points"
		set slot 33 of {_inv} to enchanted book named "&r&lLucky Miner" with lore "&7Cost: %{cost::lucky miner}% Points"
		set slot 34 of {_inv} to enchanted book named "&r&lShard Finder" with lore "&7Cost: %{cost::shard finder}% Points"
		set slot 35 of {_inv} to enchanted book named "&r&lFeed" with lore "&7Cost: %{cost::feed}% Points"
		set slot 44 of {_inv} to enchanted book named "&r&lPowerball" with lore "&7Cost: %{cost::powerball}% Points"


		# Swords
		if {sword::tier::%player%} is not set:
			set slot 37 of {_inv} to stone sword named "&r&lStone Sword" with lore "&7Cost: %{cost::stone sword}% Points"
		else if {sword::tier::%player%} is "stone":
			set slot 37 of {_inv} to iron sword named "&r&lIron Sword" with lore "&7Cost: %{cost::iron sword}% Points"
		else if {sword::tier::%player%} is "iron":
			set slot 37 of {_inv} to diamond sword named "&r&lDiamond Sword" with lore "&7Cost: %{cost::diamond sword}% Points"
		else if {sword::tier::%player%} is "diamond":
			set slot 37 of {_inv} to netherite sword named "&r&lNetherite Sword" with lore "&7Cost: %{cost::netherite sword}% Points"

		# Sword enchants
		if {sword::tier::%player%} is set:
			set slot 38 of {_inv} to enchanted book named "&r&lSharpness" with lore "&7Cost: %{cost::sharpness}% Points"
			set slot 39 of {_inv} to enchanted book named "&r&lSweeping Edge" with lore "&7Cost: %{cost::sweeping edge}% Points"
			set slot 40 of {_inv} to enchanted book named "&r&lFire Aspect" with lore "&7Cost: %{cost::fire aspect}% Points"
			set slot 41 of {_inv} to enchanted book named "&r&lKnockback" with lore "&7Cost: %{cost::knockback}% Points"

		# Armor
		if {armor::tier::%player%} is not set:
			set slot 46 of {_inv} to leather chestplate named "&r&lLeather Armor" with lore "&7Cost: %{cost::leather armor}% Points"
		else if {armor::tier::%player%} is "leather":
			set slot 46 of {_inv} to iron chestplate named "&r&lIron Armor" with lore "&7Cost: %{cost::iron armor}% Points"
		else if {armor::tier::%player%} is "iron":
			set slot 46 of {_inv} to diamond chestplate named "&r&lDiamond Armor" with lore "&7Cost: %{cost::diamond armor}% Points"

		# Armor enchants
		if {armor::tier::%player%} is set:
			set slot 47 of {_inv} to enchanted book named "&r&lProtection" with lore "&7Cost: %{cost::protection}% Points"
			set slot 48 of {_inv} to enchanted book named "&r&lThorns" with lore "&7Cost: %{cost::thorns}% Points"
on inventory click:
	if name of event-inventory is "&6&lShop":
		# Prevents the player from taking the items out of the fake inventory
		cancel event

		# General items
		if display name of event-item is "Steak":
			purchaseItem(player, "cooked beef", 16)
		else if display name of event-item is "&lLadder":
			purchaseItem(player, "ladder", 16)
		else if display name of event-item is "&lWind charges":
			purchaseItem(player, "wind charge", 8)
		else if display name of event-item is "&lAndesite":
			purchaseItem(player, "andesite", 64)
		else if display name of event-item is "&lTorches":
			purchaseItem(player, "torch", 32)
		else if display name of event-item is "&lWater Bucket":
			purchaseItem(player, "water bucket", 1)
		else if display name of event-item is "&lScaffolding":
			purchaseItem(player, "scaffolding", 32)
		else if display name of event-item is "&lTnt":
			purchaseItem(player, "tnt", 8)
		else if display name of event-item is "&lShield":
			purchaseItem(player, "shield", 1)
			
		# Pickaxes
		else if display name of event-item is "&lIron Pickaxe":
			purchasePickaxe(player, "iron pickaxe")
		else if display name of event-item is "&lDiamond Pickaxe":
			purchasePickaxe(player, "diamond pickaxe")
		else if display name of event-item is "&lNetherite Pickaxe":
			purchasePickaxe(player, "netherite pickaxe")

		# Enchants
		else if display name of event-item is "&lEfficiency":
			purchaseEnchant(player, "pickaxe", "efficiency")
		else if display name of event-item is "&lSharpness":
			purchaseEnchant(player, "sword", "sharpness")
		else if display name of event-item is "&lFire Aspect":
			purchaseEnchant(player, "sword", "fire aspect")
		else if display name of event-item is "&lSweeping Edge":
			purchaseEnchant(player, "sword", "sweeping edge")
		else if display name of event-item is "&lKnockback":
			purchaseEnchant(player, "sword", "knockback")
		else if display name of event-item is "&lProtection":
			purchaseEnchant(player, "armor", "protection")
		else if display name of event-item is "&lThorns":
			purchaseEnchant(player, "armor", "thorns")

		# Custom enchants
		else if display name of event-item is "&lVein Miner":
			purchaseCustomEnchant(player, "vein miner")
		else if display name of event-item is "&lStreak":
			purchaseCustomEnchant(player, "streak")
		else if display name of event-item is "&lOre Duplicate":
			purchaseCustomEnchant(player, "ore duplicate")
		else if display name of event-item is "&lLucky Miner":
			purchaseCustomEnchant(player, "lucky miner")
		else if display name of event-item is "&lShard Finder":
			purchaseCustomEnchant(player, "shard finder")
		else if display name of event-item is "&lFeed":
			purchaseCustomEnchant(player, "feed")
		else if display name of event-item is "&lPowerball":
			purchaseCustomEnchant(player, "powerball")

		# Custom Items
		else if display name of event-item is "&lGolden Head":
			purcahseCustomItem(player, stone, "ghead")
		else if display name of event-item is "&lGrappling Hook":
			purcahseCustomItem(player, fishing rod, "grappling hook")

		# Swords
		else if display name of event-item is "&lStone Sword":
			purchaseWeapon(player, "stone sword")
		else if display name of event-item is "&lIron Sword":
			purchaseWeapon(player, "iron sword")
		else if display name of event-item is "&lDiamond Sword":
			purchaseWeapon(player, "diamond sword")
		else if display name of event-item is "&lNetherite Sword":
			purchaseWeapon(player, "netherite sword")

		# Armor
		else if display name of event-item is "&lLeather Armor":
			purchaseArmor(player, "leather armor")
		else if display name of event-item is "&lIron Armor":
			purchaseArmor(player, "iron armor")
		else if display name of event-item is "&lDiamond Armor":
			purchaseArmor(player, "diamond armor")