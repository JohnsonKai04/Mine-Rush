# Spawns the extract by picking a 1000 x 1000 area around the initial spawn location. Once a location has been picked the Y value gets set to 310 and proceeds to check each block below said value. This makes sure that the extract beacon spawns above ground. Player has to right click the beacon inorder to extract
function spawnExtract():
	set {extractLocation::X} to a random integer between -500 and 500
	set {extractLocation::Z} to a random integer between -500 and 500
	add {spawningLocation::X} to {extractLocation::X}
	add {spawningLocation::Z} to {extractLocation::Z}
	set {extractLocation::Y} to 310
	set {_validSpawn} to location at {extractLocation::X}, {extractLocation::Y}, {extractLocation::Z} in world "world"
	wait 1 second # waits for server to catch up

	while block at {_validSpawn} is air:
		set {extractLocation::Y} to {extractLocation::Y} - 1
		set {_validSpawn} to location at {extractLocation::X}, {extractLocation::Y}, {extractLocation::Z} in world "world"

	set block at {_validSpawn} to beacon
	loop blocks within {_validSpawn} ~ vector(-1,-1,-1) and {_validSpawn} ~ vector(1,-1,1):
		set loop-block to iron block


# Param - _p: player that is playing the game
# Reutrn - returns true if game has ended successfully, false if player not in game
# Resets difficulty, tick rate, doTileDrops, bossbar, and all variables that have been changed while playing the game.
# Updates the leaderboard with the players current points, and adds overall points gained to the stats
# Clears inventoy and kills the player to return them to spawn
function endGame(p: player) :: boolean:
	if {inGame::%{_p}%} is false:
		return false
	set {inGame::%{_p}%} to false
	execute console command "/tick rate 20"

	updateLeaderboard({_p}, {points::%{_p}%})
	add {totalPoints::%{_p}%} to {stats::overallPoints::%{_p}%}
	set {pointMultiplier} to 1.0
	delete {points::%{_p}%}
	delete {totalPoints::%{_p}%}
	delete {pickaxe::tier::%{_p}%}
	delete {sword::tier::%{_p}%}
	delete {armor::tier::%{_p}%}
	delete {spawningLocation::*}
	delete {extractLocation::*}
	delete {enchantTier::*}
	delete {customEnchantTier::*}
	delete {pointBar::%{_p}%}
	delete {streak::value::%{_P}%}
	loop {activeCustomMobs::*}:
		kill loop-value
	delete {activeCustomMobs::*}
	resetEnchantPrices()
	delete boss bar with id "01"
	execute console command "/gamerule block_drops true"
	execute console command "/gamerule advance_time false"
	set the difficulty of "world" to peaceful
	clear {_p}'s inventory
	kill {_p}
	return true

# Once the player right clicks the beacon they gain invulnerability and gain a 1.5x point bonus to their current points.
# after 6 seconds endGame() runs
on right click on beacon:
	if {inGame::%player%}:
		cancel event
		make player invulnerable
		set {_bonus} to 0.5 * {points::%player%}
		add {_bonus} to {points::%player%}
		send "&4&l[Difficult Manager]: &aGood job %player% Lets pull you out! Heres a bonus for your efforts &2&l+%{_bonus}% Points" to player
		wait 6 seconds
		make player vulnerable
		add 1 to {stats::extracts::%player%}
		endGame(player)

command /endGame:
	trigger:
		if endGame(player):
			add 1 to {stats::unsuccessfulExtracts::%player%}
		else:
			send "You are currently not in the game" to player

on death of player:
	if {inGame::%victim%}:
		wait 1 tick
		add 1 to {stats::unsuccessfulExtracts::%victim%}
		add 1 to {stats::deaths::%victim%}
		endGame(victim)