# Param - _p: player that is playing the game
# Reutrn - returns true if game has ended successfully, false if player not in game
# Resets difficulty, tick rate, doTileDrops, bossbar, and all variables that have been changed while playing the game.
# Updates the leaderboard with the players current points, and adds overall points gained to the stats
# Clears inventoy and kills the player to return them to spawn
function endGame(p: player) :: boolean:
	if {inGame::%{_p}%} is false:
		return false
	delete {inGame::%{_p}%}

	updateLeaderboard({_p}, {points::%{_p}%})

	delete {difficulty::%{_p}%}
	delete {points::%{_p}%}
	delete {pickaxe::tier::%{_p}%}
	delete {sword::tier::%{_p}%}
	delete {armor::tier::%{_p}%}
	delete {spawningLocation::*}
	delete {extractLocation::*}
	delete {enchantTier::*}
	delete {customEnchantTier::*}
	delete {pointBar::%{_p}%}
	delete {streak::value::%{_P}%}
	delete {enchantCostMultiplier::%{_p}%::*}
	killCustomMobs()
	delete boss bar with id "01"

	execute console command "/gamerule mob_drops true"
	execute console command "/gamerule block_drops true"
	execute console command "/gamerule advance_time false"
	set the difficulty of "world" to peaceful

	clear {_p}'s inventory
	kill {_p}

	#stats
	set {_ID} to 0
	set {_completed} to false
	while {_completed} is false:
		add 1 to {_ID}
		if {gameStats::%{_p}%::%{_ID}%::*} is not set:
			loop {currentGameStats::%{_p}%::*}:
				set {gameStats::%{_p}%::%{_ID}%::%loop-index%} to {currentGameStats::%{_p}%::%loop-index%}
			set {gameStats::%{_p}%::%{_ID}%::timePlayed} to now
			set {_completed} to true

	loop {currentGameStats::%{_p}%::*}:
		add {currentGameStats::%{_p}%::%loop-index%} to {overallStats::%{_p}%::%loop-index%}

	delete {currentGameStats::%{_p}%::*}
	
	return true

# Once the player right clicks the beacon they gain invulnerability and gain a 1.5x point bonus to their current points.
# after 6 seconds endGame() runs
on right click on beacon:
	if {inGame::%player%} is true:
		cancel event
		make player invulnerable
		set {_bonus} to 0.5 * {points::%player%}
		add {_bonus} to {points::%player%}
		send "&4&l[Difficult Manager]: &aGood job %player% Lets pull you out! Heres a bonus for your efforts &2&l+%{_bonus}% Points" to player
		wait 6 seconds
		make player vulnerable
		add 1 to {currentGameStats::%player%::sucessfullExtracts}
		endGame(player)

command /endGame:
	trigger:
		if endGame(player) is true:
			add 1 to {currentGameStats::%player%::deaths}
		else:
			send "You are currently not in the game" to player

on death of player:
	if {inGame::%victim%} is true:
		if attacker is not set:
			add 1 to {currentGameStats::%victim%::deaths}
			endGame(victim)